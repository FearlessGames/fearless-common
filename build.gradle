apply plugin: 'java'

sourceCompatibility = 1.6
version = '0.3.5'
description = """\
Fearless-commons is a collection of java utils developed by Fearless Games
------------------------------------------------------------------------------
Project version: ${version}
Gradle version: ${gradle.gradleVersion}
------------------------------------------------------------------------------
"""

project.ext.libDir = new File("lib")

repositories {
	flatDir name: 'projectLibs', dirs: project.ext.libDir
	mavenCentral()
}

project.ext.slf4jVersion = '1.7.2'
project.ext.kahluaVersion = '2.1.0'

dependencies {
	compile group: 'com.google.guava', name: 'guava', version: '14.0'
	compile group: 'com.google.inject', name: 'guice', version: '3.0'
	compile group: 'org.slf4j', name: 'slf4j-api', version: project.ext.slf4jVersion
	compile group: 'org.mindrot', name: 'jbcrypt', version: "0.3m"
	compile group: 'se.mockachino', name: 'mockachino', version: '0.6.0'
	compile group: 'org.slf4j', name: 'jul-to-slf4j', version: project.ext.slf4jVersion
	compile group: 'se.krka', name: 'kahlua-core_5.1', version: project.ext.kahluaVersion
	compile group: 'se.krka', name: 'kahlua-j2se_5.1', version: project.ext.kahluaVersion
	compile group: 'se.krka', name: 'kahlua-interpreter_5.1', version: project.ext.kahluaVersion
	runtime group: 'org.slf4j', name: 'log4j-over-slf4j', version: project.ext.slf4jVersion
	runtime group: 'ch.qos.logback', name: 'logback-classic', version: "1.0.9"
	testCompile group: 'junit', name: 'junit', version: '4.11'

}

// Find any 3rd party libraries which have released new versions
// to the central Maven repo since we last upgraded.
task checkLibVersions << {
    def checked = [:]
    allprojects {
        configurations.each { configuration ->
            configuration.allDependencies.each { dependency ->
                def version = dependency.version
                if (!version.contains('SNAPSHOT') && !checked[dependency]) {
                    def group = dependency.group
                    def name = dependency.name
                    def url = "http://search.maven.org/solrsearch/select?q=g:\"$group\"+AND+a:\"$name\"+AND+p:\"jar\"&rows=20&wt=xml".toString()
                    try {
                        def response = new XmlSlurper().parseText(url.toURL().text)

                        def newestVersion = response.result.doc.str.findAll {
                            it.@name.text().contains("latestVersion")
                        }

                        if (!version.toString().equals(newestVersion.toString())) {
                            println "$group:$name $version -> $newestVersion"
                        }
                    } catch (FileNotFoundException e) {
                        logger.debug "Unable to download $url: $e.message"
                    } catch (org.xml.sax.SAXParseException e) {
                        logger.debug "Unable to parse $url: $e.message"
                    }
                    checked[dependency] = true
                }
            }
        }
    }
}




task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}